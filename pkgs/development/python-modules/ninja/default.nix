{ lib
, pkgs
, buildPythonPackage
, fetchPypi
, ninja-build ? pkgs.ninja
, cmake
, setuptools-scm
, setuptools
, scikit-build
}:

buildPythonPackage rec {
  pname = "ninja";
  version = "1.11.1";
  format = "pyproject";

  dontUseCmakeConfigure = true;

  src = fetchPypi {
    inherit pname version;
    sha256 = "sha256-yDOkfTmy0e7j+cqIb6FYHv1b5gaLgnNKwimWHuh0j5A=";
  };

  ninja-source = builtins.fetchTarball {
    # URL is from NinjaUrls.cmake in source
    url = "https://github.com/Kitware/ninja/archive/v1.11.1.g95dee.kitware.jobserver-1.tar.gz";
    sha256 = "sha256:1iaf17j0fclzm71f2wl12klffni4c13x8i4qz378b5wcv474ys4g";
  };

  nativeBuildInputs = [ cmake setuptools-scm setuptools scikit-build ninja-build ];

  # Patch to stop cmake from downloading ninja source
  patches = [ ./patch-cmake-download.patch ];

  # Pass ninja source to cmake
  CMAKE_ARGS = "-DNinja_SOURCE_DIR=${ninja-source}";

  meta = with lib; {
    description = "Small build system with a focus on speed";
    longDescription = ''
      Ninja is a small build system with a focus on speed. It differs from
      other build systems in two major respects: it is designed to have its
      input files generated by a higher-level build system, and it is designed
      to run builds as fast as possible.
    '';
    homepage = "https://ninja-build.org/";
    license = licenses.asl20;
    platforms = platforms.unix;
    maintainers = with maintainers; [ JonBoyleCoding ];
  };
}
